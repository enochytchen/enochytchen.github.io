% s282
282. Calculating excess and ‘avoidable’ deaths from life tables.
------
(a) Load the Melanoma data, drop subjects diagnosed 1975-1984.
```s
//==================//
// EXERCISE 282
// REVISED MAY 2015
//==================//

use melanoma11, clear
keep if year8594 == 1
stset surv_mm, fail(status==1 2) id(id) scale(12)
set trace off
strs using popmort, notables br(0(1)5) mergeby(_year sex _age) by(agegrp sex) save(replace)  

use grouped, clear
keep start end n cp cp_e2 cr_e2 sex agegrp
```
(b) What is the difference in five-year relative survival between males and females in each age group?
```s
list agegrp sex cr_e2 if end == 5, noobs sepby(agegrp)
```
(c) Reshape the data.
```s
bysort sex (agegrp start): gen j = _n
gen sexlab =cond(sex==1,"_m","_f")
drop sex
reshape wide start end n cp cp_e2 cr_e2 agegrp, i(j) j(sexlab) string
rename agegrp_m agegrp
rename start_m start
rename end_m end
drop agegrp_f start_f end_f
```

(d) For males, calculate the expected number of all-cause deaths, Nd m, the expected number of deaths if the study population were free of cancer, NExp d m and the excess deaths associated with a diagnosis of cancer, ED m.
```s
bys agegrp: gen Nrisk_m = n_m[1]/10
gen p_dead_m = 1 - cp_e2_m * cr_e2_m 
gen Nd_m = Nrisk_m*p_dead_m
gen NExp_d_m = Nrisk_m*(1-cp_e2_m)
gen ED_m = Nd_m - NExp_d_m
format Nd_m NExp_d_m ED_m %4.1f
list agegrp Nrisk_m p_dead_m Nd_m NExp_d_m ED_m if end==5, noobs
table agegrp if end == 5, c(sum Nd_m sum NExp_d_m sum ED_m) row format(%4.1f)
```
i. We would expect to see 10, 18, 28 and 22 all cause deaths in the (ascending) age groups.
ii. This is given by the excess deaths, ED m. In ascending age groups there are 9, 14, 15, and 6 excess deaths at 5 years post diagnosis when compared to a similar cancer free population.
This is for a typical cohort diagnosed in one calendar year.
iii. There are 45 excess deaths when compared to the general population.

(e) Repeat calculations for females.
```s
bys agegrp: gen Nrisk_f = n_f[1]/10
gen p_dead_f = 1 - cp_e2_f * cr_e2_f
gen Nd_f = Nrisk_f*p_dead_f
gen NExp_d_f = Nrisk_f*(1-cp_e2_f)
gen ED_f = Nd_f - NExp_d_f
format Nd_f NExp_d_f ED_f %4.1f
list agegrp Nrisk_f p_dead_f Nd_f NExp_d_f ED_f if end==5, noobs
table agegrp if end == 5, c(sum Nd_f sum NExp_d_f sum ED_f) row format(%4.1f)
```

(f) In terms of the total number of all cause deaths, females have fewer at all ages except the 70+ group. This is because they are more females diagnosed in this group 51 vs 34, so even though females have lower relative survival they have more deaths due to a number of women in the oldest age groups being diagnosed. This leads to there being more excess deaths in this age group for women when compared to men. As a whole there are more excess deaths in men.
How many deaths would be ‘avoided’ if males could achieve the same relative survival as females for Melanoma?
```s
gen Nd_m_f = Nrisk_m*(1 - cp_e2_m * cr_e2_f)
gen AD_m = Nd_m - Nd_m_f
format Nd_m_f AD_m %4.1f
list agegrp Nrisk_m p_dead_m Nd_m NExp_d_m ED_m Nd_m_f AD_m if end==5, noobs
```
There would be about 15 deaths ‘avoided’. The youngest two age groups contribute most to the avoidable deaths.

(g) List the avoidable deaths for the oldest age group over all follow-up times. Why are the number of avoidable deaths decreasing as follow-up time increases?
```s
list agegrp end AD_m if agegrp==3 
```
This is because we can not avoid deaths for ever. Remember that we are looking at all cause deaths. If we had unlimited follow-up we would avoid no deaths at all. In the oldest age group we can actually see that we are just postponing deaths.